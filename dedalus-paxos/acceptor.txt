# acceptor

######################## relation definitions
# EDB
id(id)
ballotGeq(id1, num1, id2, num2) # true if num1 >= num2 or (num1 = num2 and id1 >= id2)

# IDB
ballots(id, num, l, t) # Assumes starts with 0,0
log(payload, seq, ballotID, ballotNum, l, t)
LogSize(size, l, t)
MaxBallotNum(num, l, t)
MaxBallot(id, num, l, t)

# copied from proposer
p1a(proposerID, ballotID, ballotNum, l, t) # NOTE: not persisted
p1b(acceptorID, logSize, ballotID, ballotNum, l, t)     # NOTE: logSize necessary because p1b can't send entire log back in 1 msg
p1bLog(acceptorID, payload, seq, payloadBallotID, payloadBallotNum, ballotID, ballotNum, l, t)
p2a(proposerID, payload, seq, ballotID, ballotNum, l, t)
p2b(acceptorID, payload, seq, ballotID, ballotNum, l, t)
######################## end relation definitions



ballots(i, n, l, t') :- ballots(i, n, l, t), succ(t, t')
log(p, s, i, n, l, t') :- log(p, s, i, n, l, t), succ(t, t')



######################## reply to p1a 
ballots(id, num, l, t) :- p1a(_, id, num, l, t)
MaxBallotNum(max<num>, l, t) :- ballots(_, num, l, t) 
MaxBallot(max<id>, num, l, t) :- MaxBallotNum(num, l, t), ballots(id, num, l, t)
LogSize(count<seq>, l, t) :- log(_, seq, _, _, l, t)
p1b(i, size, ballotID, ballotNum, proposerID, t') :- p1a(proposerID, _, _, l, t), id(i), LogSize(size, l, t), MaxBallot(ballotID, ballotNum, l, t), choice(_, t')

# send back entire log 
p1bLog(i, payload, seq, payloadBallotID, payloadBallotNum, ballotID, ballotNum, proposerID, t') :- id(i), log(payload, seq, payloadBallotID, payloadBallotNum, l, t), p1a(proposerID, _, _, l, t), MaxBallot(ballotID, ballotNum, l, t), choice(_, t')
######################## end reply to p1a 



######################## reply to p2a
ballots(id, num, l, t) :- p2a(_, _, _, id, num, l, t) 
# Any rule that uses MaxBallot is guaranteed to only run once all ballots have been processed
log(payload, seq, ballotID, ballotNum, l, t) :- p2a(_, payload, seq, ballotID, ballotNum, l, t), MaxBallot(maxID, maxNum, l, t), ballotGeq(ballotID, ballotNum, maxID, maxNum)
p2b(i, payload, seq, ballotID, ballotNum, proposerID, t') :- p2a(proposerID, payload, seq, _, _, l, t), id(i), MaxBallot(ballotID, ballotNum, l, t), choice(_, t')
######################## end reply to p2a